buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
}

plugins {
    id 'org.panteleyev.jpackageplugin' version '1.3.1' apply false
    id "io.freefair.lombok" version "6.0.0-m2" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(16)
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    version = '0.0'

    ext {
        appName = 'world-trace'
        vendor = 'ca.uqam.inf6150.ete2021.gr010'
        gdxVersion = '1.10.0'
        roboVMVersion = '2.3.12'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.1.0'
        jUnitVersion = '5.7.2'
        ojdbc10Version = '19.11.0.0'
        lombokVersion = '1.18.20'
        lombokVersion = '1.18.20'
        jpaVersion = '2.2'
        ormliteVersion = '5.6'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

subprojects {
    apply plugin: 'io.freefair.lombok'

    test {
        useJUnitPlatform()

        dependencies {
            testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$jUnitVersion"
            testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "$jUnitVersion"

            testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$jUnitVersion"
        }
    }
}

project(":core") {
    apply plugin: 'java-library'

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "com.badlogicgames.ashley:ashley:$ashleyVersion"
        api group: 'javax.persistence', name: 'javax.persistence-api', version: "$jpaVersion"
        api group: 'com.j256.ormlite', name: 'ormlite-jdbc', version: "$ormliteVersion"
    }
}

project(":desktop") {
    apply plugin: 'application'
    apply plugin: 'org.panteleyev.jpackageplugin'

    dependencies {
        implementation project(":core")

        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

